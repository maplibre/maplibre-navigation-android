apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: file("${rootDir}/gradle/artifact-settings.gradle")

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
}

kotlin {
  jvmToolchain(17)
}

android {
  namespace 'org.maplibre.navigation.android.navigation.ui.v5'

  compileSdkVersion androidVersions.compileSdkVersion
  buildToolsVersion androidVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    vectorDrawables.useSupportLibrary = true

    buildConfigField('String',
        "MAPLIBRE_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "maplibre-navigation-ui-android")
    )
    buildConfigField 'String',
        "MAPLIBRE_NAVIGATION_EVENTS_USER_AGENT", String.format("\"maplibre-navigation-ui-android/%s\"",
        project.ext.versionName
    )
    consumerProguardFiles 'proguard-consumer.pro'
  }

  lint {
    baseline = file("lint-baseline.xml")
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  buildFeatures {
    buildConfig true
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
        excludes = ['jdk.internal.*']
      }
    }
  }

  publishing {
    singleVariant("release") {
      withSourcesJar()
      withJavadocJar()
    }
  }
}

dependencies {
  // Navigation SDK
  api(project(':maplibre-navigation-core'))

  api dependenciesList.mapLibreAnnotations
  implementation(dependenciesList.mapLibre)

  // Mapbox SDKs (needed for requests)
  api dependenciesList.mapboxGeoJson

  // Support libraries
  implementation dependenciesList.materialDesign
  implementation dependenciesList.androidxRecyclerView
  implementation dependenciesList.androidxConstraintLayout
  implementation dependenciesList.androidxCardView

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  implementation dependenciesList.autoValueAnnotations

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // Picasso
  implementation dependenciesList.picasso

  // Timber
  implementation dependenciesList.timber

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.robolectric
  testImplementation dependenciesList.json
  testImplementation dependenciesList.mockk
}

// Exclude old version of GeoJSON libs
// At the moment a newer version that supports Kotlin Multiplatform is required to run navigation
configurations {
  configureEach {
    exclude group: "org.maplibre.gl", module: "android-sdk-geojson"
    exclude group: "org.maplibre.gl", module: "android-sdk-turf"
  }
}


apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/publish-android.gradle"