import org.jetbrains.kotlin.gradle.plugin.mpp.apple.XCFrameworkConfig

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

apply from: file("${rootDir}/gradle/artifact-settings.gradle")

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

kotlin {
    jvmToolchain(17)

    androidTarget {
        publishLibraryVariants("release")
    }

    def xcf = new XCFrameworkConfig(project)
    [iosX64(), iosArm64(), iosSimulatorArm64()].forEach { target ->
        target.binaries.framework { framework ->
            baseName = "maplibre-navigation-core"
            xcf.add(framework)
        }
    }

    sourceSets {
        commonMain.dependencies {
            api dependenciesList.geojson
            api dependenciesList.turf

            implementation dependenciesList.kotlinxSerializationJson
            implementation dependenciesList.kotlinxCoroutines
            implementation dependenciesList.kermit
        }

        // `commonTest` can not be used here, because `mockk` is only valid for JVM targets
        androidUnitTest.dependencies {
            implementation dependenciesList.kotlinTest
            implementation dependenciesList.kotlinxCoroutinesTest
            implementation dependenciesList.mockk
        }

        androidMain.dependencies {
            implementation(dependenciesList.mapLibre)

            // Location by Play Services
            // Will only used, when added by the client app
            compileOnly dependenciesList.gmsLocation
        }
    }
}

android {
    namespace 'org.maplibre.navigation.core'

    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String',
                "MAPLIBRE_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "maplibre-navigation-android")
        )
        buildConfigField('String',
                "MAPLIBRE_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.versionName)
        )
        buildConfigField "String",
                "MAPLIBRE_NAVIGATION_EVENTS_USER_AGENT", String.format("\"maplibre-navigation-android/%s\"",
                project.ext.versionName
        )
        consumerProguardFiles 'proguard-consumer.pro'
    }

    configurations {
        javadocDeps
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    buildFeatures {
        buildConfig true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

// Exclude old version of GeoJSON libs
// At the moment a newer version that supports Kotlin Multiplatform is required to run navigation
configurations {
    configureEach {
        exclude group: "org.maplibre.gl", module: "android-sdk-geojson"
        exclude group: "org.maplibre.gl", module: "android-sdk-turf"
    }
}

//TODO (fabi755) add dokka
//apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/publish-kmp.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/dependencies-graph.gradle"
apply from: "${rootDir}/gradle/dependency-updates.gradle"
